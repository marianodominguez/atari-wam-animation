10 ;RDS12.SRC (RUN FROM ML)
20 ;DRAW RIGHT-DOWN-STEEP
30 ;DX AND DY BOTH POS
40 ;AND DX < DY
50 ;
60 ;MY OWN EQUATES
70 E=$CF
80 DX=$D0
90 DY=$D1
100 PIXZ=$DB
110 PIXLO=$DC
120 PIXHI=$DD
130 BACK=$3F
140  *=$600
150  LDA DY
160  TAX
170  INX
180  LSR A
190  STA E; E=DY/2
200  LDY #0
210 LOOP LDA PIXZ
220  ORA (PIXLO),Y; OR WITH LOC
230  STA (PIXLO),Y; LIGHT THE PIXEL
240  LDA E
250  CLC
260  ADC DX
270  STA E; E=E+DX
280  SEC
290  SBC DY; IF E<DY, VERTICAL
300  BCC INCYPIXEL
310  STA E; E=E-DY
320 ;
330 INCXPIXEL CLC
340  ROR PIXZ
350  BCC INCYPIXEL
360  ROR PIXZ
370  INC PIXLO
380  BNE INCYPIXEL
390  INC PIXHI
400 ;
410 INCYPIXEL CLC
420  LDA PIXLO
430  ADC #20
440  STA PIXLO
450  BCC NOCARRY
460  INC PIXHI
470 ;
480 NOCARRY DEX 
490  BNE LOOP
500  JMP (BACK)
