10 ;LUS12.SRC (RUN FROM ML)
20 ;DRAW LEFT-UP-STEEP
30 ;DX AND DY NEG
40 ;AND ABS(DX) < ABS(DY)
50 ;
60 ;MY OWN EQUATES
70 E=$CF
80 DX=$D0
90 DY=$D1
100 PIXZ=$DB
110 PIXLO=$DC
120 PIXHI=$DD
130 BACK=$3F
140  *=$600
150  LDA DY
160  TAX
170  INX
180  LSR A
190  STA E; E=DY/2
200  LDY #0
210 LOOP LDA PIXZ
220  ORA (PIXLO),Y; OR WITH LOC
230  STA (PIXLO),Y; LIGHT THE PIXEL
240  LDA E
250  CLC
260  ADC DX
270  STA E; E=E+DX
280  SEC
290  SBC DY; IF E<DY, VERTICAL
300  BCC DECYPIXEL
310  STA E; E=E-DY
320 ;
330 DECXPIXEL CLC
340  ROL PIXZ
350  BCC DECYPIXEL
360  ROL PIXZ
370  SEC
380  LDA PIXLO
390  SBC #1
400  STA PIXLO
410  BCS DECYPIXEL
420  DEC PIXHI
430 ;
440 DECYPIXEL SEC
450  LDA PIXLO
460  SBC #20
470  STA PIXLO
480  BCS NOBORROW
490  DEC PIXHI
500 ;
510 NOBORROW DEX 
520  BNE LOOP
530  JMP (BACK)
