10 ;LDG12.SRC (RUN FROM ML)
20 ;DRAW LEFT-DOWN-GENTLE
30 ;DX NEG AND DY POS
40 ;AND ABS(DX) > DY
50 ;
60 ;MY OWN EQUATES
70 E=$CF
80 DX=$D0
90 DY=$D1
100 PIXZ=$DB
110 PIXLO=$DC
120 PIXHI=$DD
130 BACK=$3F
140  *=$600
150  LDA DX
160  TAX
170  INX
180  LSR A
190  STA E; E=DX/2
200  LDY #0
210 LOOP LDA PIXZ
220  ORA (PIXLO),Y; OR WITH LOC
230  STA (PIXLO),Y; LIGHT THE PIXEL
240  LDA E
250  CLC
260  ADC DY
270  STA E; E=E+DY
280  SEC
290  SBC DX; IF E<DX, FLAT
300  BCC DECXPIXEL
310  STA E; E=E-DX
320 ;
330 INCYPIXEL CLC
340  LDA PIXLO
350  ADC #20
360  STA PIXLO
370  BCC DECXPIXEL
380  INC PIXHI
390 ;
400 DECXPIXEL CLC
410  ROL PIXZ
420  BCC SAMEBYTE
430  ROL PIXZ
440  LDA PIXLO
450  SEC
460  SBC #1
470  STA PIXLO
480  BCS SAMEBYTE
490  DEC PIXHI
500 ;
510 SAMEBYTE DEX
520  BNE LOOP
530  JMP(BACK)
